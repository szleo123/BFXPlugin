// MEL GUI for the BFX Maya plug-in

// Add BFX menu item to Maya menu bar
global proc createMenu()
{	
	global string $gMainWindow;
	
	// check if the menu already exists.
	// 	BFXMenu is a menu object
	if(`menu -ex BFXMenu`)
	{
		deleteUI BFXMenu;
	}
	
	setParent $gMainWindow;
	menu -l "BFX" BFXMenu;
		menuItem -l "Mesh Preparation" -c displayConfig;
		menuItem -l "Brittle Fracture" -c createCompoundNode;
}

global proc deleteMenu()
{
	if(`menu -ex BFXMenu`)
	{
		deleteUI BFXMenu;
	}
}

global proc deleteChildrenOfSelected() 
{
    // Get the list of currently selected objects
    string $selectedObjects[] = `ls -sl`;
    string $object = $selectedObjects[0]; 
    // List all children of the current object
    string $children[] = `listRelatives -c $object`;
    for ($i = 1; $i < size($children); $i++){
        delete $children[$i];
    }
    select -cl; 
}

// Create cmd configuration dialog window
global proc displayConfig()
{
	global string $numIter;
	global string $patternType = "patternType";
	//global string $fractureDensity;
	global string $explodeAmt;
    
	if(`window -ex BFXWindow`)
	{
		deleteUI BFXWindow;
	}

	window -title "Brittle Fracture Simulation Options" -wh 500 500 BFXWindow;

	// Node placement
	// collapsable parameters
	frameLayout -collapsable true -label "Node Placement";
		columnLayout -adjustableColumn true;
		rowLayout -numberOfColumns 3 -ad3 1; 
		intSliderGrp -l "Num of Nodes" -f true -fmn 0 -fmx 50 -min 1 -max 100 -v 10 "numSlider";
		button -l "Place Nodes" -c "int $num = `intSliderGrp -q -v numSlider`; createBox($num)";
		button -l "Delete Nodes" -c deleteChildrenOfSelected;
		setParent ..;
		
		//string $NodePlacecmd = "int $val = `intSliderGrp -q -v " + $numIter + "`; NodeCmd -number $val;";
		//string $nodeplacerBtn = `button -l "Place Nodes" -c $NodePlacecmd"`; 

		text -l "Voronoi Container Size:" -fn boldLabelFont;
		//attrFieldGrp -attribute ($sphere[0] + ".translate") -label "Min Sites";
		//attrFieldGrp -attribute ($sphere[0] + ".scale") -label "Max Sites";
		
		string $formGrammar = `formLayout`;
		string $title = `text -l "CoACD" -fn boldLabelFont`;
		string $browseBtn = `button -l "Pre-process" -c runCoACD`;

		formLayout -e
			-attachForm $title "left" 100
			-attachForm $browseBtn "right" 100
			$formGrammar;
		setParent ..;

	setParent ..;

	// Fracture pattern
	// collapsable parameters
	frameLayout -collapsable true -label "Fracture Configuration";
		columnLayout -adjustableColumn true;

		// create the first radio collection
 		$radio1 = `radioCollection`;

 		// add some radio buttons to the collection
 		$full = `radioButton -label "Full-body"`;
 		$partial = `radioButton -label "Partial"`;
		// edit the radio collections to set the required radio button
 		radioCollection -edit -select $full $radio1;

		optionMenu -label "Fracture Pattern" -w 100 $patternType; // -changeCommand "print #1" // this will print out the curr selection @ dropdown.onChange()
            menuItem -label "Uniform";
            menuItem -label "Cluster";
            menuItem -label "Radial";
			menuItem -label "Plannar";
			menuItem -label "Slice";
			menuItem -label "Brick";
			menuItem -label "Custom";
		string $formGrammar2 = `formLayout`;
		string $title2 = `text -l "Custom Fracture Pattern" -fn boldLabelFont`;
		string $browseBtn2 = `button -l "Browse File" -c openFileBrowser`;

		formLayout -e -en false
			-attachForm $title2 "left" 100
			-attachForm $browseBtn2 "right" 100
			$formGrammar2;
		setParent ..;

		$explodeAmt = `floatSliderGrp -l "Explode Amount" -f true -fmn 0 -fmx 5 -min 0 -max 1 -v 0.5`;
		//$fractureDensity = `floatSliderGrp -l "Fracture Density" -f true -fmn 0 -fmx 5 -min 0 -max 5 -v 1`;
	setParent ..;

	// Partial Fracturing
	// collapsable parameters
	frameLayout -collapsable true -label "Partial Fracturing";
		columnLayout -adjustableColumn true;

		text -l "Impact Sphere:" -fn boldLabelFont;
		//attrFieldGrp -attribute ($sphere[0] + ".translate") -label "Position" -en false;
		//attrFieldGrp -attribute ($sphere[0] + ".scale") -label "Scale" -en false;
	setParent ..;

	// dialog buttons
	string $formParams = `formLayout`;
	string $createBtn = `button -l "Create" -w 50 -c "float $explode = `floatSliderGrp -q -v $explodeAmt`; callFractureCmd($explode)"`;
	string $cancelBtn = `button -l "Cancel" -w 50 -c quitConfig`;

	formLayout -e
		-attachPosition $createBtn "left" 0 75
		-attachPosition $cancelBtn "right" 0 100
		$formParams;

	showWindow;
}

global proc runCoACD()
{
	// Triangulate selected mesh first
	string $selected[] = `ls -sl`;
	select -r $selected[0];
	polyTriangulate -ch on $selected[0];
	select -r $selected[0];
	
	PreprocessCmd;
}

// call FractureCmd with args from config window
global proc callFractureCmd(float $explode)
{
	global string $patternType;

	string $currPatternType = `optionMenu -q -v $patternType`;
	print ("pattern: " + $currPatternType + "\n");
	
	print("explode amount: "+$explode+ "\n");
	
	FractureCmd -p $patternType -e $explode;
}

// Close command window
global proc quitConfig()
{
	if(`window -ex BFXWindow`)
	{
		deleteUI BFXWindow;
	}
}

global proc createCompoundNode()
{
	// set original mesh invisible
	select -allDagObjects -visible;
	toggleVisibilityAndKeepSelection `optionVar -query toggleVisibilityAndKeepSelectionBehaviour`;

	createNode transform -n Compound1;
	createNode mesh -n CompoundShape1 -p Compound1;
	sets -add initialShadingGroup CompoundShape1;
	createNode CompoundNode -n CompoundNode1;
	//connectAttr time1.outTime LSystemNode1.time;
	connectAttr CompoundNode1.outMesh CompoundShape1.inMesh;
	polySeparate -ch 1 CompoundShape1;

	// add active rigid bodies to the shards
	select -r Compound1;
	string $shapes[] = `listRelatives -c`;

	// create a gravity field with default settings
	gravity -pos 0 0 0 -m 9.8 -att 0 -dx 0 -dy -1 -dz 0  -mxd -1  -vsh none -vex 0 -vof 0 0 0 -vsw 360 -tsr 0.5 ;
	
	// Note: the last element of the Compound is a transform so we don't add rigid body to that.
	for ($i=0; $i<size($shapes)-1; ++$i)
	{
		select -r $shapes[$i];
		rigidBody -active -m 1 -dp 0 -sf 0.2 -df 0.2 -b 0.6 -l 0 -tf 200 -iv 0 0 0 -iav 0 0 0 -c 0 -pc 0 -i 0 0 0 -imp 0 0 0 -si 0 0 0 -sio none ;
		connectDynamic -f gravityField1 $shapes[$i];

		// reset face normals for a smooth appearance
		expandPolyGroupSelection;polySetToFaceNormal ; 
	};

	// Optional:
	// Create a ground plane for collision
	polyPlane -w 50 -h 50 -sx 10 -sy 10 -ax 0 1 0 -cuv 2 -ch 1;
	move -r 0 -3 0; // translate along y-axis
	
	// Add passive rigid body to it
	rigidBody -passive -m 1 -dp 0 -sf 0.2 -df 0.2 -b 0.6 -l 0 -tf 200 -iv 0 0 0 -iav 0 0 0 -c 0 -pc 0 -i 0 0 0 -imp 0 0 0 -si 0 0 0 -sio none ;
	
	// Begin playback from min to max timeframe
	// play -forward true;
}

global proc createBox(int $numOfNodes)
{
    // Check if one mesh is selected
    string $selection[] = `ls -sl -type "transform"`;
    if(size($selection) != 1) {
        error "Please select exactly one mesh.";
    }

    string $mesh = $selection[0];
    xform -cp $mesh;
    float $translationMesh[] = `xform -q -os -t $mesh`;
    float $scaleMesh[] = `xform -q -os -s $mesh`;
    xform -os -t 0 0 0 $mesh; 
    xform -os -s 1 1 1 $mesh; 
    // Create bounding box as wireframe and set as child of the mesh
    float $bbox[] = `polyEvaluate -b $mesh`;
    string $bboxName = $mesh + "_bbox";
    string $bboxCtrl = `curve -n $bboxName -d 1 -p $bbox[0] $bbox[2] $bbox[4] -p $bbox[1] $bbox[2] $bbox[4] -p $bbox[1] $bbox[3] $bbox[4] -p $bbox[0] $bbox[3] $bbox[4] -p $bbox[0] $bbox[2] $bbox[4] -p $bbox[0] $bbox[2] $bbox[5] -p $bbox[1] $bbox[2] $bbox[5] -p $bbox[1] $bbox[3] $bbox[5] -p $bbox[0] $bbox[3] $bbox[5] -p $bbox[0] $bbox[2] $bbox[5] -p $bbox[0] $bbox[3] $bbox[5] -p $bbox[0] $bbox[3] $bbox[4] -p $bbox[1] $bbox[3] $bbox[4] -p $bbox[1] $bbox[3] $bbox[5] -p $bbox[1] $bbox[2] $bbox[5] -p $bbox[1] $bbox[2] $bbox[4]`;
    transformLimits -tx $bbox[0] $bbox[1] -ty $bbox[2] $bbox[3] -tz $bbox[4] $bbox[5] -etx 1 1 -ety 1 1 -etz 1 1 ;
    parent $bboxCtrl $mesh;

    // Create 10 little spheres within the bounding box
    float $sphereRadius = min(min($bbox[1] - $bbox[0], $bbox[3] - $bbox[2]), $bbox[5] - $bbox[4]) * 0.02;
    float $meshScale[] = `xform -q -ws -s $mesh`;
    float $boxScale[] = `xform -q -ws -s $bboxCtrl`;
    print($scaleMesh);
    //float $scaleFactor[] = {1.0 / $parentScale[0], 1.0 / $parentScale[1], 1.0 / $parentScale[2]}
    for ($i = 0; $i < $numOfNodes; $i++) {
        string $sphereName = $mesh + "_node" + $i;
        string $sphere[] = `polySphere -n $sphereName -r $sphereRadius`;
        parent $sphere[0] $bboxCtrl;
        float $randX = rand($bbox[0], $bbox[1]);
        float $randY = rand($bbox[2], $bbox[5]);
        float $randZ = rand($bbox[3], $bbox[4]);
        move -r $randX $randY $randZ; 
        transformLimits -tx $bbox[0] $bbox[1] -ty $bbox[2] $bbox[3] -tz $bbox[4] $bbox[5] -etx 1 1 -ety 1 1 -etz 1 1 ;
    }

    xform -os -s $scaleMesh[0] $scaleMesh[1] $scaleMesh[2] $mesh; 
    xform -os -t $translationMesh[0] $translationMesh[1] $translationMesh[2] $mesh; 
}


createMenu();